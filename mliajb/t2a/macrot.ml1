ML/I test -- MACROTIME -- version T2A
Tests macro expressions and macro-time statements
Macros tested are: MC-GO,MC-SET,MCSUB,MCLENG,MC-PVAR,MC-NOTE
MCDEF T2VAL AS %T2.
MCINS%.
T2VAL SB 3 (This will be wrong if extra calls have been added)
MCGO ERROR PVARS UNLESS%P1.%P2.%P3.%P4.%P5.=00000
MCGO  ERROR PVARS UNLESS%P6.%P7.%P8.%P9.%P10.=00000
MCSET P10=2
MCSKIP MT,<>
MCSKIP NONL WITHS NL
MCSET P1=6
MCSUB(X1X,MCLENG(AB),MCLENG(A)+1 )MCSUB(X23,P1/3,0)NONL
MCSUB(4X,-1,1)MCSUB(MCSUB (5XX ,1,-1),1,-1) SB 12345
MCSUB(,1,1)MCSUB( A ,1,2)MCSUB(AB,2,1)MCSUB(A,1,-1)NONL
MCSUB(A,P1,P1)MCLENG()MCLENG(MCLENG(A)) SB 01
MCSET P1=3
MCSET SP1=3
MCSET P2=20
MCSET PS3=1
%P3.%PP1 +1.%+4*+--3-27/3.%16/2/2.%-P2 /-4.%PPPP1+5. SB 123456
MCSET MCSET P4=5
 PP4=MCSET P6=%%1..
% %%P6...
%7/4.%4/4.%3/4.%-1/-4.%3/-4.%-1/4.%-8/4.%7/-4. SB 1100-1-1-2-2
SUCCESSFUL GOTOS
MCGO L1
1%L1.MCGO L1 UNLESS 1+1 EN 1
2%L1.MCGO L%1234.IF=
3%L1234.MCGO L MCGO L1
X%L1.1 IF P1 GE P1-1
4%L1.MCGO L1 UNLESS XXA =XXB
5%L1.MCGO L1 IF P3 GE P3
6%L1.MCGO L2-1 IF P3+1 GE P3
7%L1.NOW TRY UNSUCCESSFUL GOS
MCGO LA IF<A >=A
MCGO LB UNLESS 1 EN 1
MCGO LC IF 60 GR %60.
MCGO LD IF 6 GR 7
MCGO LE IF 71 GE 72
TRY GOS WITHIN MACRO
MCDEF GOMACRO; AS<MCGO L1
% L 2.%T1-1.MCGO L3
%%A1..MCGO L2
%L3.%TP1.MCSET T3=T3+1
MCGO L3 IF 3 GE T3
MCGO L0
%L3.>
GOMACRO L1; SB 0123
MCPVAR 50
MCPVAR%5+5.
%P50.%P11+1.%P10.%P1. SB 0123
TEST BC ON MC-GO
1<>MCGO L1 IF A BC L
1%L1.MCGO L1 IF ZZAZ BC<  L  >
2%L1.MCGO L1 IF 0090 BC N
3%L1.MCGO L1 IF-+0123456789 BC N
4%L1 .MCGO L1 UNLESS Z90A-BC I
5%L1.MCGO L1 IF ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789 BC I
6%L1.MCGO LL IF A0 BC L
MCGO LM IF BC I
MCGO LN IF+-BC N
MCGO LP IF+BC L
MCGO LQ IF 1+1 BC N
MCNOTE This is a MC-NOTE message from line %S2+1. of source text
MCDEF NOTEST;
AS<MCNOTE A MC-NOTE within the macro <NOTEST>
%A1.MCSET S4=1
MCNOTE No environment should be printed with this MC-NOTE
>
NOTEST MCNOTE A MC-NOTE within an argument to <NOTEST>
;%3+%2.-%1.-2. SB 12
